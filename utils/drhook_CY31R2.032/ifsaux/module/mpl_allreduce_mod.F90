MODULE MPL_ALLREDUCE_MOD

!**** MPL_ALLREDUCE Perform collective communication

!     Purpose.
!     --------
!     To calculate global MIN,MAX,SUM or IEOR and return result to all processes.
!     The data may be REAL*4, REAL*8,or INTEGER, one dimensional array or scalar

!**   Interface.
!     ----------
!        CALL MPL_ALLREDUCE

!        Input required arguments :
!        -------------------------
!           PSENDBUF -  buffer containing message to be collectively communicated
!                       (can be type REAL*4, REAL*8 or INTEGER) (also output)
!           CDOPER   -  Global operation to be performed : 'MAX', 'MIN', 'SUM' or 'IEOR'

!        Input optional arguments :
!        -------------------------
!           LDREPROD -  Reproducibility flag for SUMmation-operator.
!                       Meaningful only for REAL-numbers.
!                       Three modes (applicable for REAL-number only):
!                       1) Not provided at all (the default) ==> MPL_ABORT
!                       2) Provided and .TRUE. ==> PROC#1 collects partial data
!                          from every processor and does the summation in order
!                          and finally MPL_BROADCASTs result to every processor.
!                          No MPI_ALLREDUCE used.
!                       3) Provided, but .FALSE. ==> let MPI_ALLREDUCE do the summation.
!           KCOMM    -  Communicator number if different from MPI_COMM_WORLD 
!                       or from that established as the default 
!                       by an MPL communicator routine
!                       the incoming data
!           CDSTRING -  Character string for ABORT messages
!                       used when KERROR is not provided

!        Output required arguments :
!        -------------------------
!           none

!        Output optional arguments :
!        -------------------------
!           KERROR   -  return error code.     If not supplied, 
!                       MPL_ALLREDUCE aborts when an error is detected.
!     Author.
!     -------
!        D.Dent, M.Hamrud, S.Saarinen     ECMWF

!     Modifications.
!     --------------
!        Original: 2001-02-02

!     ------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB     ,JPRM,  JPIB

USE MPL_MPIF
USE MPL_DATA_MODULE
USE MPL_MESSAGE_MOD
USE MPL_SEND_MOD
USE MPL_RECV_MOD
USE MPL_BROADCAST_MOD

IMPLICIT NONE

PRIVATE

LOGICAL :: LLABORT=.TRUE.

INTERFACE MPL_ALLREDUCE
MODULE PROCEDURE MPL_ALLREDUCE_REAL8, MPL_ALLREDUCE_REAL4, MPL_ALLREDUCE_INT, &
  MPL_ALLREDUCE_INT8, &
  MPL_ALLREDUCE_REAL8_SCALAR, MPL_ALLREDUCE_REAL4_SCALAR, &
  MPL_ALLREDUCE_INT_SCALAR, MPL_ALLREDUCE_INT8_SCALAR
END INTERFACE

PUBLIC MPL_ALLREDUCE

CONTAINS

SUBROUTINE MPL_ALLREDUCE_INT_SCALAR(KSENDBUF,CDOPER,LDREPROD, &
                            & KCOMM,KERROR,CDSTRING)

INTEGER(KIND=JPIM),INTENT(INOUT)     :: KSENDBUF
CHARACTER(LEN=*),INTENT(IN)    :: CDOPER
logical,INTENT(IN),OPTIONAL :: LDREPROD
INTEGER(KIND=JPIM),INTENT(IN),OPTIONAL :: KCOMM
INTEGER(KIND=JPIM),INTENT(OUT),OPTIONAL :: KERROR
CHARACTER(LEN=*),INTENT(IN),OPTIONAL :: CDSTRING
INTEGER(KIND=JPIM) ISENDBUF(1)
INTEGER(KIND=JPIM) :: ISENDCOUNT,IRECVCOUNT,ICOMM,IERROR,IOPER

ISENDBUF(1) = KSENDBUF
CALL MPL_ALLREDUCE(ISENDBUF,CDOPER,LDREPROD,KCOMM,KERROR,CDSTRING)
KSENDBUF = ISENDBUF(1)

END SUBROUTINE MPL_ALLREDUCE_INT_SCALAR

SUBROUTINE MPL_ALLREDUCE_INT8_SCALAR(KSENDBUF,CDOPER,LDREPROD, &
                            & KCOMM,KERROR,CDSTRING)
INTEGER(KIND=JPIB),INTENT(INOUT)     :: KSENDBUF
CHARACTER(LEN=*),INTENT(IN)    :: CDOPER
logical,INTENT(IN),OPTIONAL :: LDREPROD
INTEGER(KIND=JPIM),INTENT(IN),OPTIONAL :: KCOMM
INTEGER(KIND=JPIM),INTENT(OUT),OPTIONAL :: KERROR
CHARACTER(LEN=*),INTENT(IN),OPTIONAL :: CDSTRING
INTEGER(KIND=JPIB) ISENDBUF(1)
INTEGER(KIND=JPIM) :: ISENDCOUNT,IRECVCOUNT,ICOMM,IERROR,IOPER

ISENDBUF(1) = KSENDBUF
CALL MPL_ALLREDUCE(ISENDBUF,CDOPER,LDREPROD,KCOMM,KERROR,CDSTRING)
KSENDBUF = ISENDBUF(1)

END SUBROUTINE MPL_ALLREDUCE_INT8_SCALAR



SUBROUTINE MPL_ALLREDUCE_REAL8_SCALAR(PSENDBUF,CDOPER,LDREPROD, &
                            & KCOMM,KERROR,CDSTRING)
REAL(KIND=JPRB),INTENT(INOUT)     :: PSENDBUF
CHARACTER(LEN=*),INTENT(IN)    :: CDOPER
logical,INTENT(IN),OPTIONAL :: LDREPROD
INTEGER(KIND=JPIM),INTENT(IN),OPTIONAL :: KCOMM
INTEGER(KIND=JPIM),INTENT(OUT),OPTIONAL :: KERROR
CHARACTER(LEN=*),INTENT(IN),OPTIONAL :: CDSTRING
REAL(KIND=JPRB) ZSENDBUF(1)
INTEGER(KIND=JPIM) :: ISENDCOUNT,IRECVCOUNT,ICOMM,IERROR,IOPER

ZSENDBUF(1) = PSENDBUF
CALL MPL_ALLREDUCE(ZSENDBUF,CDOPER,LDREPROD,KCOMM,KERROR,CDSTRING)
PSENDBUF = ZSENDBUF(1)

END SUBROUTINE MPL_ALLREDUCE_REAL8_SCALAR


SUBROUTINE MPL_ALLREDUCE_REAL4_SCALAR(PSENDBUF,CDOPER,LDREPROD, &
                            & KCOMM,KERROR,CDSTRING)
REAL(KIND=JPRM),INTENT(INOUT)     :: PSENDBUF
CHARACTER(LEN=*),INTENT(IN)    :: CDOPER
logical,INTENT(IN),OPTIONAL :: LDREPROD
INTEGER(KIND=JPIM),INTENT(IN),OPTIONAL :: KCOMM
INTEGER(KIND=JPIM),INTENT(OUT),OPTIONAL :: KERROR
CHARACTER(LEN=*),INTENT(IN),OPTIONAL :: CDSTRING
REAL(KIND=JPRM) ZSENDBUF(1)
INTEGER(KIND=JPIM) :: ISENDCOUNT,IRECVCOUNT,ICOMM,IERROR,IOPER

ZSENDBUF(1) = PSENDBUF
CALL MPL_ALLREDUCE(ZSENDBUF,CDOPER,LDREPROD,KCOMM,KERROR,CDSTRING)
PSENDBUF = ZSENDBUF(1)

END SUBROUTINE MPL_ALLREDUCE_REAL4_SCALAR


SUBROUTINE MPL_ALLREDUCE_INT(KSENDBUF,CDOPER,LDREPROD, &
                            & KCOMM,KERROR,CDSTRING)

#ifdef USE_8_BYTE_WORDS
  Use mpi4to8, Only : &
    MPI_ALLREDUCE => MPI_ALLREDUCE8
#endif

INTEGER(KIND=JPIM),INTENT(INOUT)     :: KSENDBUF(:)
CHARACTER(LEN=*),INTENT(IN)    :: CDOPER
logical,INTENT(IN),OPTIONAL :: LDREPROD
INTEGER(KIND=JPIM),INTENT(IN),OPTIONAL :: KCOMM
INTEGER(KIND=JPIM),INTENT(OUT),OPTIONAL :: KERROR
CHARACTER(LEN=*),INTENT(IN),OPTIONAL :: CDSTRING
INTEGER(KIND=JPIM) :: IRECVBUF(SIZE(KSENDBUF))
INTEGER(KIND=JPIM) :: ISENDCOUNT,IRECVCOUNT,ICOMM,IERROR,IOPER
INTEGER(KIND=JPIM) :: ITID
ITID = OML_MY_THREAD()
IF(MPL_NUMPROC < 1) CALL MPL_MESSAGE( &
  & CDMESSAGE='MPL_ALLREDUCE: MPL NOT INITIALISED ',LDABORT=LLABORT) 

IF(CDOPER(1:3) == 'MAX' .OR. CDOPER(1:3) == 'max' ) THEN
  IOPER = MPI_MAX
ELSEIF(CDOPER(1:3) == 'MIN' .OR. CDOPER(1:3) == 'min' ) THEN
  IOPER = MPI_MIN
ELSEIF(CDOPER(1:3) == 'SUM' .OR. CDOPER(1:3) == 'sum' ) THEN
  IOPER = MPI_SUM
ELSEIF(CDOPER(1:4) == 'IEOR' .OR. CDOPER(1:4) == 'ieor' ) THEN
  IOPER = MPI_BXOR
ELSE
  CALL MPL_MESSAGE(IERROR,'MPL_ALLREDUCE: ERROR UNKNOWN OPERATOR',&
   & CDSTRING,LDABORT=LLABORT)
ENDIF

IF(PRESENT(KCOMM)) THEN
  ICOMM=KCOMM
ELSE
  ICOMM=MPL_COMM_OML(ITID)
ENDIF

ISENDCOUNT = SIZE(KSENDBUF)

CALL MPI_ALLREDUCE(KSENDBUF,IRECVBUF,ISENDCOUNT,INT(MPI_INTEGER), &
                &  IOPER,ICOMM,IERROR)

IF(MPL_OUTPUT > 1 )THEN
  WRITE(MPL_UNIT,'(A,5I8)') ' MPL_ALLREDUCE ',ISENDCOUNT,ICOMM,IOPER
ENDIF
IF(PRESENT(KERROR)) THEN
  KERROR=IERROR
ELSE
  IF(IERROR /= 0 ) CALL MPL_MESSAGE(IERROR,'MPL_ALLREDUCE',CDSTRING,LDABORT=LLABORT)
ENDIF

KSENDBUF(:) = IRECVBUF(:)

END SUBROUTINE MPL_ALLREDUCE_INT

SUBROUTINE MPL_ALLREDUCE_INT8(KSENDBUF,CDOPER,LDREPROD, &
                            & KCOMM,KERROR,CDSTRING)
!J INTEGER(KIND=JPIM),INTENT(INOUT)     :: KSENDBUF(:)
   INTEGER(KIND=JPIB),INTENT(INOUT)     :: KSENDBUF(:)
CHARACTER(LEN=*),INTENT(IN)    :: CDOPER
logical,INTENT(IN),OPTIONAL :: LDREPROD
INTEGER(KIND=JPIM),INTENT(IN),OPTIONAL :: KCOMM
INTEGER(KIND=JPIM),INTENT(OUT),OPTIONAL :: KERROR
CHARACTER(LEN=*),INTENT(IN),OPTIONAL :: CDSTRING
!J INTEGER(KIND=JPIM) :: IRECVBUF(SIZE(KSENDBUF))
   INTEGER(KIND=JPIB) :: IRECVBUF(SIZE(KSENDBUF)), KMAX
INTEGER(KIND=JPIM) :: ISENDCOUNT,IRECVCOUNT,ICOMM,IERROR,IOPER,JJ
INTEGER(KIND=JPIM) :: ITID
ITID = OML_MY_THREAD()
IF(MPL_NUMPROC < 1) CALL MPL_MESSAGE( &
  & CDMESSAGE='MPL_ALLREDUCE: MPL NOT INITIALISED ',LDABORT=LLABORT) 

IF(CDOPER(1:3) == 'MAX' .OR. CDOPER(1:3) == 'max' ) THEN
  IOPER = MPI_MAX
ELSEIF(CDOPER(1:3) == 'MIN' .OR. CDOPER(1:3) == 'min' ) THEN
  IOPER = MPI_MIN
ELSEIF(CDOPER(1:3) == 'SUM' .OR. CDOPER(1:3) == 'sum' ) THEN
  IOPER = MPI_SUM
  KMAX=2*(1024_JPIB**6-1)
  DO JJ=1,SIZE(KSENDBUF)
    IF(KSENDBUF(JJ) > KMAX/MPL_NUMPROC .OR. &
       KSENDBUF(JJ) < -KMAX/MPL_NUMPROC) THEN
      CALL MPL_MESSAGE(IERROR,&
       & 'MPL_ALLREDUCE: SUMMATION MAY EXCEED 64bit INTEGER LIMIT',&
       &CDSTRING,LDABORT=LLABORT)
    ENDIF
  ENDDO
ELSEIF(CDOPER(1:4) == 'IEOR' .OR. CDOPER(1:4) == 'ieor' ) THEN
  IOPER = MPI_BXOR
ELSE
  CALL MPL_MESSAGE(IERROR,'MPL_ALLREDUCE: ERROR UNKNOWN OPERATOR',&
   & CDSTRING,LDABORT=LLABORT)
ENDIF

IF(PRESENT(KCOMM)) THEN
  ICOMM=KCOMM
ELSE
  ICOMM=MPL_COMM_OML(ITID)
ENDIF

ISENDCOUNT = SIZE(KSENDBUF)

CALL MPI_ALLREDUCE(KSENDBUF,IRECVBUF,ISENDCOUNT,MPI_INTEGER8, &
                &  IOPER,ICOMM,IERROR)

IF(MPL_OUTPUT > 1 )THEN
  WRITE(MPL_UNIT,'(A,5I8)') ' MPL_ALLREDUCE ',ISENDCOUNT,ICOMM,IOPER
ENDIF
IF(PRESENT(KERROR)) THEN
  KERROR=IERROR
ELSE
  IF(IERROR /= 0 ) CALL MPL_MESSAGE(IERROR,'MPL_ALLREDUCE',CDSTRING,LDABORT=LLABORT)
ENDIF

KSENDBUF(:) = IRECVBUF(:)

END SUBROUTINE MPL_ALLREDUCE_INT8


SUBROUTINE MPL_ALLREDUCE_REAL8(PSENDBUF,CDOPER,LDREPROD, &
                            & KCOMM,KERROR,CDSTRING)


#ifdef USE_8_BYTE_WORDS
  Use mpi4to8, Only : &
    MPI_ALLREDUCE => MPI_ALLREDUCE8
#endif

REAL(KIND=JPRB),INTENT(INOUT)        :: PSENDBUF(:)
CHARACTER(LEN=*),INTENT(IN)    :: CDOPER
logical,INTENT(IN),OPTIONAL :: LDREPROD
INTEGER(KIND=JPIM),INTENT(IN),OPTIONAL :: KCOMM
INTEGER(KIND=JPIM),INTENT(OUT),OPTIONAL :: KERROR
CHARACTER(LEN=*),INTENT(IN),OPTIONAL :: CDSTRING
REAL(KIND=JPRB)            :: ZRECVBUF(SIZE(PSENDBUF))
INTEGER(KIND=JPIM) IPROC, ITAG, ICOUNT
LOGICAL LLREPRODSUM
INTEGER(KIND=JPIM) :: ISENDCOUNT,IRECVCOUNT,ICOMM,IERROR,IOPER
INTEGER(KIND=JPIM) :: ITID
ITID = OML_MY_THREAD()
LLREPRODSUM = .FALSE.

IF(MPL_NUMPROC < 1) CALL MPL_MESSAGE( &
  & CDMESSAGE='MPL_ALLREDUCE: MPL NOT INITIALISED ',LDABORT=LLABORT) 

IF(CDOPER(1:3) == 'MAX' .OR. CDOPER(1:3) == 'max' ) THEN
  IOPER = MPI_MAX
ELSEIF(CDOPER(1:3) == 'MIN' .OR. CDOPER(1:3) == 'min' ) THEN
  IOPER = MPI_MIN
ELSEIF(CDOPER(1:3) == 'SUM' .OR. CDOPER(1:3) == 'sum' ) THEN
  IOPER = MPI_SUM
  IF (PRESENT(LDREPROD)) THEN
    LLREPRODSUM = LDREPROD
  ELSE
    CALL MPL_MESSAGE(IERROR,&
     & 'MPL_ALLREDUCE: SUMMATION OPERATOR NOT REPRODUCIBLE IN REAL MODE',&
     & CDSTRING,LDABORT=LLABORT)
  ENDIF
ELSE
  CALL MPL_MESSAGE(IERROR,'MPL_ALLREDUCE: ERROR UNKNOWN OPERATOR',&
   & CDSTRING,LDABORT=LLABORT)
ENDIF

IF(PRESENT(KCOMM)) THEN
  ICOMM=KCOMM
ELSE
  ICOMM=MPL_COMM_OML(ITID)
ENDIF

ISENDCOUNT = SIZE(PSENDBUF)
IRECVCOUNT = SIZE(ZRECVBUF)

IF (LLREPRODSUM) THEN
!-- Near reproducible summation
  ITAG = 2001
  IF (MPL_RANK == 1) THEN
    DO IPROC=2,MPL_NUMPROC
      CALL MPL_RECV(ZRECVBUF,KSOURCE=IPROC,KCOMM=ICOMM,KTAG=ITAG,&
        &KERROR=IERROR,KOUNT=ICOUNT)
      IF (ICOUNT /= IRECVCOUNT) THEN
        WRITE(MPL_ERRUNIT,'(A,I10,A,I6,A,I10)')&
        & 'MPL_ALLREDUCE: RECEIVED UNEXPECTED NUMBER OF ELEMENTS ', &
        & ICOUNT,' FROM PROC ',IPROC,'. EXPECTED=',IRECVCOUNT
        CALL MPL_MESSAGE(IERROR,'MPL_ALLREDUCE',CDSTRING,LDABORT=LLABORT)
      ENDIF
      PSENDBUF(:) = PSENDBUF(:) + ZRECVBUF(:)
    ENDDO
    ZRECVBUF(:) = PSENDBUF(:)
  ELSE
    CALL MPL_SEND(PSENDBUF,KDEST=1,KCOMM=ICOMM,KTAG=ITAG,KERROR=IERROR,&
      &KMP_TYPE=JP_BLOCKING_STANDARD,CDSTRING='MPLS_SEND')
  ENDIF
  ITAG = ITAG + 1
  CALL MPL_BROADCAST(ZRECVBUF,KTAG=ITAG,KCOMM=ICOMM,KROOT=1,KERROR=IERROR)
ELSE  
  CALL MPI_ALLREDUCE(PSENDBUF,ZRECVBUF,ISENDCOUNT,INT(MPI_REAL8), &
                  &  IOPER,ICOMM,IERROR)
ENDIF

IF(MPL_OUTPUT > 1 )THEN
  WRITE(MPL_UNIT,'(A,5I8)') ' MPL_ALLREDUCE ',ISENDCOUNT,IRECVCOUNT,ICOMM,IOPER
ENDIF
IF(PRESENT(KERROR)) THEN
  KERROR=IERROR
ELSE
  IF(IERROR /= 0 ) CALL MPL_MESSAGE(IERROR,'MPL_ALLREDUCE',CDSTRING,LDABORT=LLABORT)
ENDIF

PSENDBUF(:) = ZRECVBUF(:)

END SUBROUTINE MPL_ALLREDUCE_REAL8


SUBROUTINE MPL_ALLREDUCE_REAL4(PSENDBUF,CDOPER,LDREPROD, &
                            & KCOMM,KERROR,CDSTRING)


#ifdef USE_8_BYTE_WORDS
  Use mpi4to8, Only : &
    MPI_ALLREDUCE => MPI_ALLREDUCE8
#endif

REAL(KIND=JPRM),INTENT(INOUT)        :: PSENDBUF(:)
CHARACTER(LEN=*),INTENT(IN)    :: CDOPER
logical,INTENT(IN),OPTIONAL :: LDREPROD
INTEGER(KIND=JPIM),INTENT(IN),OPTIONAL :: KCOMM
INTEGER(KIND=JPIM),INTENT(OUT),OPTIONAL :: KERROR
CHARACTER(LEN=*),INTENT(IN),OPTIONAL :: CDSTRING
REAL(KIND=JPRM)            :: ZRECVBUF(SIZE(PSENDBUF))
INTEGER(KIND=JPIM) IPROC, ITAG, ICOUNT
LOGICAL LLREPRODSUM
INTEGER(KIND=JPIM) :: ISENDCOUNT,IRECVCOUNT,ICOMM,IERROR,IOPER
INTEGER(KIND=JPIM) :: ITID
ITID = OML_MY_THREAD()
LLREPRODSUM = .FALSE.

IF(MPL_NUMPROC < 1) CALL MPL_MESSAGE( &
  & CDMESSAGE='MPL_ALLREDUCE: MPL NOT INITIALISED ',LDABORT=LLABORT) 

IF(CDOPER(1:3) == 'MAX' .OR. CDOPER(1:3) == 'max' ) THEN
  IOPER = MPI_MAX
ELSEIF(CDOPER(1:3) == 'MIN' .OR. CDOPER(1:3) == 'min' ) THEN
  IOPER = MPI_MIN
ELSEIF(CDOPER(1:3) == 'SUM' .OR. CDOPER(1:3) == 'sum' ) THEN
  IOPER = MPI_SUM
  IF (PRESENT(LDREPROD)) THEN
    LLREPRODSUM = LDREPROD
  ELSE
    CALL MPL_MESSAGE(IERROR,&
     & 'MPL_ALLREDUCE: SUMMATION OPERATOR NOT REPRODUCIBLE IN REAL MODE',&
     & CDSTRING,LDABORT=LLABORT)
  ENDIF
ELSE
  CALL MPL_MESSAGE(IERROR,'MPL_ALLREDUCE: ERROR UNKNOWN OPERATOR',&
   & CDSTRING,LDABORT=LLABORT)
ENDIF

IF(PRESENT(KCOMM)) THEN
  ICOMM=KCOMM
ELSE
  ICOMM=MPL_COMM_OML(ITID)
ENDIF

ISENDCOUNT = SIZE(PSENDBUF)
IRECVCOUNT = SIZE(ZRECVBUF)

IF (LLREPRODSUM) THEN
!-- Near reproducible summation
  ITAG = 2001
  IF (MPL_RANK == 1) THEN
    DO IPROC=2,MPL_NUMPROC
      CALL MPL_RECV(ZRECVBUF,KSOURCE=IPROC,KCOMM=ICOMM,KTAG=ITAG,&
        &KERROR=IERROR,KOUNT=ICOUNT)
      IF (ICOUNT /= IRECVCOUNT) THEN
        WRITE(MPL_ERRUNIT,'(A,I10,A,I6,A,I10)')&
        & 'MPL_ALLREDUCE: RECEIVED UNEXPECTED NUMBER OF ELEMENTS ', &
        & ICOUNT,' FROM PROC ',IPROC,'. EXPECTED=',IRECVCOUNT
        CALL MPL_MESSAGE(IERROR,'MPL_ALLREDUCE',CDSTRING,LDABORT=LLABORT)
      ENDIF
      PSENDBUF(:) = PSENDBUF(:) + ZRECVBUF(:)
    ENDDO
    ZRECVBUF(:) = PSENDBUF(:)
  ELSE
    CALL MPL_SEND(PSENDBUF,KDEST=1,KCOMM=ICOMM,KTAG=ITAG,KERROR=IERROR,&
      &KMP_TYPE=JP_BLOCKING_STANDARD,CDSTRING='MPLS_SEND')
  ENDIF
  ITAG = ITAG + 1
  CALL MPL_BROADCAST(ZRECVBUF,KTAG=ITAG,KCOMM=ICOMM,KROOT=1,KERROR=IERROR)
ELSE  
  CALL MPI_ALLREDUCE(PSENDBUF,ZRECVBUF,ISENDCOUNT,INT(MPI_REAL4), &
                  &  IOPER,ICOMM,IERROR)
ENDIF

IF(MPL_OUTPUT > 1 )THEN
  WRITE(MPL_UNIT,'(A,5I8)') ' MPL_ALLREDUCE ',ISENDCOUNT,IRECVCOUNT,ICOMM,IOPER
ENDIF
IF(PRESENT(KERROR)) THEN
  KERROR=IERROR
ELSE
  IF(IERROR /= 0 ) CALL MPL_MESSAGE(IERROR,'MPL_ALLREDUCE',CDSTRING,LDABORT=LLABORT)
ENDIF

PSENDBUF(:) = ZRECVBUF(:)

END SUBROUTINE MPL_ALLREDUCE_REAL4

END MODULE MPL_ALLREDUCE_MOD
