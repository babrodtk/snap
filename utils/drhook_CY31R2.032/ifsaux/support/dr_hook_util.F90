SUBROUTINE DR_HOOK_UTIL(CDNAME,KCASE,PKEY,CDFILENAME,KSIZEINFO)
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK,ONLY : LHOOK
USE YOMOML,ONLY : OML_MAX_THREADS,OML_MY_THREAD
USE MPL_INIT_MOD
USE MPL_ARG_MOD
IMPLICIT NONE
CHARACTER(LEN=*),INTENT(IN) :: CDNAME,CDFILENAME
INTEGER(KIND=JPIM),INTENT(IN) :: KCASE,KSIZEINFO
REAL(KIND=JPRB),INTENT(INOUT) :: PKEY
#ifdef RS6K
INTEGER(KIND=JPIM) :: INEWMASK, IOLDMASK, UMASK
#endif

LOGICAL,SAVE :: LL_FIRST_TIME = .TRUE.
CHARACTER(LEN=256),SAVE :: CLENV
INTEGER(KIND=JPIM) INUMTIDS, IMYTID

! -----------------------------------------------------------------

IF (.NOT.LHOOK) RETURN
IF (LL_FIRST_TIME) THEN
  LL_FIRST_TIME = .FALSE.
  CALL MPL_INIT(LDINFO=.FALSE.) ! Do not produce any output
  CALL EC_GETENV('DR_HOOK',CLENV)
  IF (CLENV == ' ' .OR. CLENV == '0' .OR. &
    & CLENV == 'false' .OR. CLENV == 'FALSE') THEN
    LHOOK = .FALSE.
    CALL C_DRHOOK_SET_LHOOK(0)
    RETURN
  ENDIF
  CALL MPL_GETARG(0, CLENV)
  INUMTIDS = OML_MAX_THREADS()
  CALL C_DRHOOK_INIT(CLENV, INUMTIDS)
ENDIF
IMYTID = OML_MY_THREAD()
IF (KCASE == 0) THEN
  CALL C_DRHOOK_START(CDNAME, IMYTID, PKEY, CDFILENAME, KSIZEINFO)
ELSE IF (KCASE == 1) THEN
  CALL C_DRHOOK_END  (CDNAME, IMYTID, PKEY, CDFILENAME, KSIZEINFO)
ENDIF

END SUBROUTINE DR_HOOK_UTIL
